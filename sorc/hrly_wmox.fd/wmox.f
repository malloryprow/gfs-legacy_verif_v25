C$$$  MAIN PROGRAM DOCUMENTATION BLOCK
C
C MAIN PROGRAM: wmox
C   PRGMMR: LILLY            ORG: NP12        DATE: 2005-06-21
C
C ABSTRACT: Verifies gfs vs ssi analysis for 3 regions:
C              1.  nrn hemisphere 20N to pole                            
C              2.  srn hemisphere 20S tp pole                           
C              3.  tropics between 20S and 20N  
C           Verification is done on a 2.5 degree lat-lon grid.
C
C PROGRAM HISTORY LOG:
C
C UNKNOWN      Charles Vlcek -- ORIGINAL AUTHOR
C 2005-06-21 STEVEN G. LILLY -- UPDATING SOURCE CODES
C 2007-05-02 Krishna V Kumar -- Modified to extend the fcst
C                               hours to 240 by changing  
C                               the mod function to take
C                               the correct unit numbers
C                               of the input forecast/analysis
C                               GRIB files  
C
C 2008-11-20 Krishna V Kumar -- Fixed the conflict in the 
C                               GRIBFILE unit numbers and 
C                               wrong value for the 240h forecast
C USAGE:
C   INPUT FILES:
C    UNIT 11 - date of verification time
C    UNIT 12 - pds values of verification levels and fields
C    UNIT 13 - verification data (model, truth, hours, etc)
C
C   OUTPUT FILES:
C
C   SUBPROGRAMS CALLED:
C
C   EXIT STATES:
C     COND =   0 - SUCCESSFUL RUN
C
C REMARKS:
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE : IBM-SP
C
C$$$
C
C          DATA SET WMOXMAIN   AT LEVEL 011 AS OF 09/01/94
C
C..................................................................
C..  WMO VFCN  ..... MEAN ERROR, RMS ERROR, CORREL COEFF OF       .
C..    CHANGES, AND S1 SCORES                                     .
C..  26OCT90...ADDED 5.0DEG LATLON VFCN.                          .
C..  APR94...SET ID(12)=99 TO REDUCE NUM STMTS GENERATED BY       .
C..          KEYSERS 36-DAY ARCHIVE RETRIEVAL CODE.               .
c..  began conversion to cray on 9/26/96                          .
c..  mostly completed 10/8/96 (except for some cosmetics)         .
c..                                                               .
c..  changed to read from daily grib files                        .
c..                                                               .
c..  made y2k compliant, compiled on f90 9/??/98  -- C. Vlcek     .
c..  compiled on IBM RS6000 9/16/99 -- C. Vlcek                   .
c..                                                               .
c..  program verifies gfs vs avn ssi analysis for 3 regions:      .
c..    1.  nrn hemisphere 20N to pole                             .
c..    2.  srn hemisphere 20S tp pole                             .
c..    3.  tropics between 20S and 20N                            .
c..                                                               .
c..  verification is done on a 2.5 degree lat-lon grid.           .
c..                                                               .
c..  1 deg grib fields are interpolated to 2.5 deg with Mark      .
c..  Iredell's routines to remain consistent with previous scores .
c..                                                               .
c..  only verification out to day 9 is done due to the lack of    .
c..  day 10 forecast files stored in scom at the current time     .
c..                                                               .
c..  the 5.0 deg latlon verification has been removed as not      .
c..  necessary                                                    .
C..................................................................
C
c  input files:
c    ft11 - date of verification time
c    ft12 - pds values of verification levels and fields
c    ft13 - verification data (model, truth, hours, etc)
c
c  output files:
c
c
       PARAMETER    (MXCYL=1,MXFCT=10,MXLVL=4,mxmdl=2)
       CHARACTER(10) JRUN(2)
       CHARACTER(10) ITRUTH
       CHARACTER(8) PLVL,PLEVEL
       CHARACTER(6) NAMRGN(3)
       CHARACTER(3) JMDL(2),JANL(2),NAME,NTRU,parm
       REAL         WEIGHT(37,3), SX(15,MXCYL,MXFCT,2),X(15,2)
       INTEGER      NFCTS(2)
       integer      idate(mxcyl),ivt(4,mxcyl)
       LOGICAL(1)   IVFCT(mxfct,mxmdl),IVLVL(mxlvl),IVMDL(2)
C
C
       DATA  ICON1 / 1000000000 /
       DATA  IDISK /20/
       DATA  IGRDNH,IGRDSH /29,30/
       DATA  IGRDA,IGRDF /29,29/
       DATA  NAMRGN /'NO HEM','SO HEM','TROPCS'/
C
       COMMON /XDATA/ IDATE,IDS(4,MXLVL),IVFCT,IVT,
     1                IVLVL,PLVL(MXLVL)
       COMMON /YDATA/ NBEGIN,NEND,ICYB,ICYE,ICYI,IVFLD,IFTINC,
     1                NICR,JLVLS,JFCTS,MDANL,ITRUTH
C
C
C
       CALL IFILL(IDS,4,MXLVL,1,0)
       CALL AFILL(WEIGHT,37,3,1,99.9)
       DO 8 I = 1,2
       CALL AFILL(SX(1,1,1,I),15,MXCYL,MXFCT,999.9)
   8   CONTINUE
C
C
C...................................................................
C.....READ IN DATA, PRINT, AND INITIALIZE ARRAYS.....
C...................................................................
C
 110  FORMAT(I10)
 120  FORMAT(1X,4L1,2I3,1X,2L1,4I3,1X,A9,1X,I1)
 125  FORMAT('LEVELS........ MODEL...    FLD         MDL ',
     1       /,' IV        J  N IV  J  N FT IV TRUTH    ANL')
 130  FORMAT(1X,A3,1X,A9,1X,A3,1X,10L1,I3)
 140  FORMAT(I3,3X,3I3,3X,2I3,3X,I3)
 200  FORMAT(15X,'****** LABELS ******')
 210  FORMAT('  *** VERIFICATION PERIOD ',I10,' TO ' ,I10,' *** ',
     1       /,10X,'PRESSURE LEVELS = ',4L1,',  TOTAL = ',
     2       I2,', TO DO= ',I2,' .... MODELS = ',2L1,',  TOTAL= ',
     3       I2,', TO DO= ',I2,/, 15X,'.....TOTAL FCSTS = ',I2,
     4       5X,'..... VERIFYING FLD = ',I2,2X,A9,3X,'(MODEL =',I2,
     5       ')',//)
 220  FORMAT(10X,'.....IDENT  = ',I2,' ... ',4i7,5X,' ... ',a6,'...')
 230  FORMAT(10X,'.....MODEL ',I2,2X,A3,' ... ',A9,
     1       ' RUN ... ',A3,' ANALYSIS ..... ',/,30X,'FCSTS = ',10L1,
     2       ' .. TOTAL OF ',I2,' TIMES .. TO DO = ',I2)
 240   FORMAT(5X,'  VERIFY CYCLES (BEGIN,END,INCREMENT)= ',3I3,
     1        ' ... FCST INCREMENT= ',I3,
     2        ' ... BEGIN RGN= ',I2,'  END REGN= ',I2,/,40X,
     3        '.....REGION 1,2,3 = ', 3(A6,3X),/,40X,
     4        '.....VERIFICATION(1=2.5LATLON,2=2.5 AND 5.0LATLON)',
     5        '= ',I2,' .....',//)
C
 245   FORMAT(10X,'CYCLE=',I3,4X,'YR/MN/DY/HR = ',4I6,8X,
     1       'IDATE = ',i10)
C
 250   FORMAT(10X,'CYCLE=',I3,4X,'YR/MN/DY/HR = ',4I6,8X,
     1       'IDATE = ',i8)
C
 260   FORMAT(10X,'...........LAST CYCLE DATE = ',i10,4X,
     1        'REQUESTED END DATE = ',i10,4X,'TOTAL CYCLES= ',I4,///)
 270   FORMAT(10X,'.....TOTAL NUMBER OF POINTS.......',
     1        T46,'2.5DEG LATLON',65X,'5.0DEG LATLON',/,
     2        15X,'N HEM  LAT=9,37,LON=1,144..... ', I8,12X, I8,/,
     3        15X,'S HEM  LAT=1,29,LON=1,144..... ', I8,12X, I8,/,
     4        15X,'TRPCS  LAT=1,17,LON=1,144..... ', I8,12X, I8,///)
 300   FORMAT(5X,'..... COSINE WEIGHTS FOR REGIONS .....')
 310   FORMAT(5X,'REGION= ',I4, ( 13F8.4) )
C
C..... . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
C
       READ(11,110,END=900) ndate
         NBEGIN=ndate
         NEND=ndate
C
       PRINT 200
       DO 10 J = 1,MXLVL
       READ(12,*,END=900) (IDS(I,J),I=1,4),PLVL(J)
        PRINT 220,J, (IDS(K,J),K=1,4),PLVL(J)
  10   CONTINUE
C
C
       READ(13,120,END=900) IVLVL,JLVLS,NLVLS,IVMDL,JMDLS,NMDLS,
     1                      JFCTS,IVFLD,ITRUTH,MDANL
       PRINT 125
       PRINT 120, IVLVL,JLVLS,NLVLS,IVMDL,JMDLS,NMDLS,JFCTS,
     1            IVFLD,ITRUTH,MDANL
       PRINT 210, NBEGIN,NEND,IVLVL,JLVLS,NLVLS,IVMDL,JMDLS,NMDLS,
     1            JFCTS,IVFLD,ITRUTH,MDANL
       DO 12 I = 1,JMDLS
       READ(13,130,END=900) JMDL(I),JRUN(I),JANL(I),
     1             (IVFCT(J,I),J=1,JFCTS),NFCTS(I)
       PRINT 230,I,JMDL(I),JRUN(I),JANL(I),(IVFCT(J,I),J=1,JFCTS),
     1             JFCTS,NFCTS(I)
  12   CONTINUE
C
       READ(13,140,END=900) IFTINC,ICYB,ICYE,ICYI,IRGNB,IRGNE,NICR
       PRINT 240, ICYB,ICYE,ICYI,IFTINC,IRGNB,IRGNE,
     1            (NAMRGN(I),I=IRGNB,IRGNE),NICR
C
C
C....................................................................
C.....1. SETUP VERIFICATION TIME TABLE.....
C.....2. DEFINE WEIGHT (COSINE LAT....1D AND 1E MAPS)........
C         REGION 3 (TROPICS) IS 20S TO 20 N
C....................................................................
C
C
        CALL IFILL(IDATE,MXCYL,1,1,0)
        CALL IFILL(IVT,4,MXCYL,1,0)
C
c  for a daily verification, nbegin=nend=idate
c
        KOUNT = 0
        KBEGIN = nBEGIN
C
        DO 20 IJ = 1,MXCYL
          INCR = IJ - 1
          KOUNT = KOUNT + 1
          idate(ij)=kbegin
C
          IF ( IDATE(IJ) .LT. ICON1 ) THEN
C
C           DATE FORMAT: YYMMDDHH
C
            CALL DATECNV8(IDATE(IJ),IVT(1,IJ),IVT(2,IJ),
     1                    IVT(3,IJ),  IVT(4,IJ))
C
            PRINT 250,IJ,IVT(1,IJ),IVT(2,IJ),IVT(3,IJ),IVT(4,IJ),
     1               IDATE(IJ)
C
          ELSE
C
C           DATE FORMAT: YYYYMMDDHH
C  
            CALL DATECNV10( IDATE(IJ), IVT(1,IJ), IVT(2,IJ),
     1                     IVT(3,IJ),  IVT(4,IJ))
            PRINT 245,IJ,IVT(1,IJ),IVT(2,IJ),IVT(3,IJ),IVT(4,IJ),
     1                IDATE(IJ)
C
          END IF
C
          IF(IDATE(IJ).EQ.nEND) GO TO 22
  20    CONTINUE
C
  22    CONTINUE
        PRINT 260,IDATE(IJ),nEND,KOUNT
C
        NPNH1 = ((37-9) + 1) * ((144-1) + 1)
        NPSH1 = ((29-1) + 1) * ((144-1) + 1)
        NPTR1 = ((17-1) + 1) * ((144-1) + 1)
        NPNH2 = ((37-9)/2  +  1) * ((144-1)/2  +  1)
        NPSH2 = ((29-1)/2  +  1) * ((144-1)/2  +  1)
        NPTR2 = ((17-1)/2  +  1) * ((144-1)/2  +  1)
        PRINT 270,NPNH1,NPNH2,NPSH1,NPSH2,NPTR1,NPTR2
C
C.....
        DO 24  J = 1,37
         XLAT = 2.5*(J-1)/57.296
         JJ = 37 - J + 1
         WEIGHT(J,1) = COS(XLAT)
         WEIGHT(JJ,2) = WEIGHT(J,1)
  24    CONTINUE
C
        DO 26  J = 1,9
         JJ = J + 28
         WEIGHT(J,3) = WEIGHT(JJ,2)
  26    CONTINUE
C
        DO 28  J = 10,17
         JJ = J - 8
         WEIGHT(J,3) = WEIGHT(JJ,1)
  28    CONTINUE
C
       PRINT 300
       DO 50 J = 1,3
        PRINT 310,J,(WEIGHT(I,J),I=1,37)
  50   CONTINUE
C
C
C....................................................................
C..... 1. DO daily VERIFICATION, PRINT SUMMARY TABLES ...............
C..... 2. PRINT Daily DATA ..........................................
c..... 3. monthly summary of scores is done in program xsum.f........
C....................................................................
C
C..... . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
C
       DO 30  MODEL = 1,JMDLS
       NTRU = JMDL(MDANL)
       NAME = JMDL(MODEL)
       IF(.NOT. IVMDL(MODEL)) GO TO 30
C
 320   FORMAT(///,5X,'..... ',A3,' (M=',I1,') VERIFIED AGNST ',a9,1x,A3,1X,
     1        ' (IVFLD=',I1,') ... ',I2,' LEVELS, ',I3,' FCSTS ',
     2        '... CYCLE BEGIN,END,INCREM= ',3I4,' .....',/,25X,
     3        '... IRGN BEGIN AND IRGN END ',2I4,' .....',//)
c
       PRINT 320,NAME,MODEL,jrun(2),janl(2),IVFLD,JLVLS,JFCTS,
     1           ICYB,ICYE,ICYI,IRGNB,IRGNE
C
C....
       DO 40 LVL = 1,JLVLS
       PLEVEL = PLVL(LVL)
C
        IF(.NOT. IVLVL(LVL)) GO TO 40
        CALL WMOEVL(MODEL,NAME,LVL,WEIGHT,IRGNB,IRGNE,MXCYL,
     1              MXFCT,SX)
C
C.....
C
  40   CONTINUE
C
  30   CONTINUE
C
C
 900   CONTINUE
C
C
       STOP
       END
