
      SUBROUTINE ADPFIL(IER)

C$$$  SUBPROGRAM DOCUMENTATION BLOCK                                    
C                .      .    .                                       .  
C SUBPROGRAM:    ADPFIL      READ PREPROCESSED RADIOSONDE DATA          
C   PRGMMR: VLCEK            ORG: W/NP12   DATE: 99-05-26 
C
C ABSTRACT: READS RADIOSONDE DATA FROM SEQUENTIAL FILE FT09F001
C   WHICH WAS WRITTEN BY PROGRAM QCADP (SUBROUTINE /ADPTOS/). 
C   THIS DATA HAS ALREADY PASSED VARIOUS QUALITY CHECKS AND WAS
C   SELECTED BY LEVEL (850, 500, 250, & 100 MB) AND QUANTITY  
C   (Z, T, RH, U, AND V).
C
C PROGRAM HISTORY LOG: 
C   87-09-23  VLCEK   
C   89-01-25  VLCEK       CHANGE FROM FORTRAN 66 TO VS FORTRAN 77 
C   96-06-25  Y. ZHANG    MOVE IT TO CRAY AND REFINE IT.
C   98-07-13  VLCEK       COMPILE IN F90 AND MAKE Y2K-COMPLIANT
C   99-05-26  VLCEK       CONVERT FOR USE ON IBM RS6000.
C 
C USAGE:    CALL ADPFIL(IER)  
C   INPUT ARGUMENT LIST:  NONE                                          
C                                                                       
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)                  
C     IER      - RETURN CODE; 0 = SUCCESSFUL DATA RETRIEVAL;            
C                1 = END OF FILE ENCOUNTERED OR DATE READ IS            
C                LATER THAN ANY INDICATED DESIRED DATE(S).              
C                                                                       
C   INPUT FILES:                                                        
C     FT09F001 - SEQUENTIAL FILE CONTAINING PREPROCESSED RADIO-         
C                SONDE DATA.  EACH OBSERVATION DATE/TIME HAS A          
C                SET OF RECORDS; THE FIRST HAS THE MAP TYPE OF          
C                ANALYSIS USED IN TOSSOUTS, NUMBER OF RECORDS           
C                (STATIONS) TO FOLLOW FOR THAT OBSERVATION, THE         
C                PACKED AND UNPACKED DATE/TIME, AND SUNDRY OTHER        
C                VARIABLES PERTAINING TO THE ANALYSIS MAP.  EACH        
C                SUCCEEDING RECORD CONTAINS A STATION I.D. NUMBER,      
C                ITS LONGITUDE AND LATITUDE, ITS I-J MAP CO-ORD         
C                NUMBERS, THE I.D. NUMBER OF THE STATION NEAREST        
C                TO IT (FOR S1 CALCULATION PURPOSES) AND ITS            
C                DISTANCE FROM THAT STATION (IN KM), AND FINALLY,       
C                THE QUALITY-CHECKED VALUES OF Z, T, RH, U, AND V       
C                AT 850, 500, 250, AND 100 MB.                          
C                                                                       
C   OUTPUT FILES:                                                       
C     FT06F001 - STATUS AND ERROR MESSAGES ONLY.  IF SUCCESSFUL         
C                RETURN: DATE, MAP TYPE OF ANALYSIS USED IN             
C                TOSSOUTS, AND NUMBER OF STATIONS WILL BE PRINTED.      
C                                                                       
C REMARKS: MOST DATA IS PASSED IN COMMON BLOCKS.  SEE DOCBLOCK IN       
C          MAIN PROGRAM TO DETERMINE DATE SELECTION PROCESS (FT05,      
C          NAMELIST /OPTION/).  REFER TO SOURCE CODE FOR PROGRAM        
C          QCADP FOR CONSTRUCTION OF DATA FOUND IN FT09; NOTE THAT      
C          IN OPERATIONAL FILES THE STORED ADP DATA HAS ALSO PASSED     
C          THROUGH THE RADIATION CORRECTION ROUTINE VIA PROGRAM         
C          RADNCORR.  ARCHIVE FILES MORE THAN A MONTH OLD ARE           
C          AVAILABLE ON TAPE -- SEE C. VLCEK, W/NP12
C                                                                       
C ATTRIBUTES:                                                           
C   LANGUAGE: VS FORTRAN 90  IBM FORTRAN  
C   MACHINE:  IBM RS6000
C                                                                       
C$$$                                                                    
C                                                                       
C          THIS SUBROUTINE READS A PROCESSED UPPER AIR DATA FILE        
C     DESIGNED FOR USE BY PROGRAM ACUMVER4 FOR VERIFYING FORECASTS      
C     AGAINST THE UPPER AIR DATA VIA SUMAC CODE.  OTHER USERS ARE       
C     WELCOME TO THE DATA BUT MUST REALIZE THAT IT CONTAINS ONLY FOUR   
C     LEVELS (850, 500, 250, AND 100 MB) OF HEIGHT, TEMPERATURE, R.H.,  
C     AND WIND DATA.                                                    
C                                                                       
C          THIS DATA HAS BEEN CORRECTED FOR THE EFFECTS OF SOLAR        
C     RADIATION ON THE RADIOSONDE INSTRUMENT PACKAGE USING PROGRAM      
C     RADNCORR, AND IS THE OUTPUT OF PROGRAM QCADP, WHICH FETCHES       
C     THE DATA, CHECKS FOR DUPLICATES AND OUT-OF-RANGE VALUES (EITHER   
C     METEOROLOGICAL OR POSITIONAL), CHECKS REPORT TYPE, IDENTIFIES     
C     QUALITY MARKERS, AND CHECKS AGAINST THE CONCURRENT ANALYSIS.      
C     THE CHECK AGAINST THE ANALYSIS INVOLVES DETERMINING THE STANDARD  
C     DEVIATION OF THE DIFFERENCES BETWEEN THE RADIOSONDE VALUE AND     
C     THE ANALYSIS VALUE; IF THE DEVIATION FOR A PARTICULAR RADIO-      
C     SONDE VALUE FROM THE CORRESPONDING ANALYSIS VALUE EXCEEDS A       
C     GIVEN THRESHHOLD, THAT RADIOSONDE VALUE IS THROWN OUT (BY         
C     ASSIGNING A MISSING VALUE OF -99999.).                            
C                                                                       
C          IN ADDITION, A NEIGHBOR IS ASSIGNED TO EACH STATION, FOR     
C     THE PURPOSE OF COMPUTING S1 SCORES;  THE NEIGHBOR STATION IDENT   
C     AND THE DISTANCE BETWEEN THE TWO ARE SAVED.  ALSO THE I,J GRID    
C     VALUES OF EACH STATION WITH RESPECT TO THE ANALYSIS GRID, ALONG   
C     WITH THE ANALYSIS MAP NUMBER (K VALUE), IS STORED.  IF A DIFFER-  
C     ENT MAP IS USED FOR VERIFICATION, THE I,J VALUES ARE RECOMPUTED   
C     (IN SUBROUTINE PARTSM, WHICH CALLS SUBROUTINE STIJ).              
C                                                                       
C          THE FIRST RECORD FOR A GIVEN OBSERVATION DATE/TIME CONTAINS  
C     THE KMAP VALUE (EXPRESSED AS A NEGATIVE NUMBER TO IDENTIFY THE    
C     RECORD AS A HEADER), THE NUMBER OF STATIONS/RECORDS TO FOLLOW,    
C     AND THE PACKED AND UNPACKED DATE WORDS.                           
C                                                                       
C          FINALLY IT SHOULD BE NOTED THAT STATION IDENT NUMBERS        
C     GREATER THAN 99999 REFER TO MOVING SHIPS AND ARE CODED LATITUDE-  
C     LONGITUDE VALUES.                                                 
C                                                                       
C                                                                       
CC                                                                      

      IMPLICIT  INTEGER(G)

C     /G/ IS RESERVED FOR CONSTANTS NAMED IN A PARAMETER STATEMENT   
C     SEE SUBROUTINE ACCUMU FOR LIST AND EXPLANATIONS OF NAMES.      

      PARAMETER  (GORFIL=22, GNVAR=4, GVECTV=4)
      PARAMETER  (GQ1=7, GQ2=11, GQ3=52, GQ4=33, GQ5=34)
      PARAMETER  (GQ6=0, GQ7=0, GQ8=0, GQ9=0, GQ10=0)

C     GQ1 IS HEIGHT, GQ2 IS TEMPERATURE, GQ3 IS RELATIVE HUMIDITY.    
C     GQ4 IS U-COMPONENT OF WIND; V-COMPONENT IS ASSUMED TO BE GQ4+1. 
C     REMAINING GQ VALUES ARE RESERVED FOR FUTURE USE.                

      PARAMETER  (GNVARP=(GNVAR+1), GNLEV=4)
      PARAMETER  (GP1=0, GP2=850, GP3=0, GP4=500, GP5=0, GP6=0)
      PARAMETER  (GP7=250, GP8=0, GP9=0, GP10=100, GP11=0, GP12=0)
      PARAMETER  (GP13=0, GP14=0, GP15=0)
      PARAMETER  (GIMAX=360, GJMAX=181, GMXDIM=(GIMAX*GJMAX) )
      PARAMETER  (GMXSTN=1100, GMINBN=0, GMAXBN=99999)
      PARAMETER  (GNERQU=10, GNGRQU=6, GACCUM=(GNERQU*2) )
      PARAMETER  (GMXTBL=600, GSVBL=12,  GNMAPS=17)
      PARAMETER  (GMXARA=15, GMXRUN=15)

      COMMON /ADPSTN/ IDSTN(GMXSTN)    , ALON(GMXSTN)  , ALAT(GMXSTN) ,
     &                NRPT(GMXSTN)     , DISTNN(GMXSTN),
     &                ZTRUV(GMXSTN,GNVARP,GNLEV),
     &                STI(GMXSTN), STJ(GMXSTN)  , JSTA
      COMMON /MAPINF/ NSTA, KMAP , IMXMAP       , JMXMAP,
     &                NCYCMP     , F9, DATENL
      COMMON /CFDATE/ IAPPCK     , IAPUNP(4)
      COMMON /AREAS / ALOLA(GMXARA,4)  , AREA(GMXARA)  ,
     &                KSTN(GMXARA)     , STNLST(GMXARA , GMXTBL)
      COMMON /RAFILE/ FORFIL(GORFIL)   , NFILE         , GCODE ,
     &                DATA(GMXDIM)     , DATA1(GMXDIM) , MINWND,
     &                MAXWND, IGRID    , VDATE1, VDATE2,
     &                VDATE , DIAG     , PRSTAT, WRSTAT,
     &                INTPM , KRUN     , FHR(GORFIL)
      COMMON /UNITS / IUAREA, IUADP    , IUGBD(GORFIL) , IUGBI(GORFIL) ,
     &                IUJET , IUSTA    , IUOUT  , IUOPN, IUTAB

      REAL         MINWND , MAXWND, DUMMY(14), ALOLA
      INTEGER      VDATE1 , VDATE2 , VDATE , RPT      , STNLST, GCODE
      INTEGER      FLAG   , IER    , IADP  , IGRID    , NFILE
      CHARACTER*8  FORFIL , AREA
      CHARACTER*4  KRUN   , FHR
      LOGICAL      PRSTAT , WRSTAT , DIAG

      IER  = 0
      JERR = 0
      IF(DIAG) PRINT 9900
 9900 FORMAT (T15,'BEGIN SUBROUTINE ADPFIL ....')
    1 CONTINUE
      READ (IUADP,ERR=4,END=5)  FLAG, JSTA, IAPPCK, IAPUNP,
     &      DATENL, NPT, IMXMAP, JMXMAP, NCYCMP, F9, DUMMY
C     IF (DIAG) PRINT 1001, FLAG, JSTA, IAPUNP
C1001 FORMAT (' FLAG, JSTA, IAPUNP =', 2I8, 2X,4I3)
C    
C     CONVERT QCADP DATE TO INCLUDE CENTURY IF NECESSARY 
C     (SUCH AS WHEN USING OLD QCADP FILES FOR CASE STUDIES)
C    
      IF (FLAG.GE.0) GO TO 1
      IF (IAPUNP(1).LT.100)    THEN
        IAPUNP(1) = IAPUNP(1) + 1900
        IAPPCK = IAPPCK + 1900000000
        DATENL = DATENL + 1900000000.
      ENDIF
      NCEN  = IAPUNP(1)/100
      VDATE = IAPPCK
C    
C     IF DATE SELECTION FROM NAMELIST VARIABLES VDATE1 AND VDATE2 DO NOT
C     INCLUDE CENTURY INFO, ASSUME "CURRENT" (QCADP) CENTURY FOR BOTH AND
C     ADD IT ON.  NORMALLY THE QCADP FILE USED FOR OPERATIONAL VERIFICATION
C     WILL CONTAIN JUST ONE OBSERVATION PERIOD, HENCE VDATE1 AND VDATE2
C     ARE USUALLY SET SO THAT ANY DATE WILL PASS.
C    
      IF (VDATE1.LT.99999999)    THEN
        VDATE1 = VDATE1 + 100000000*NCEN
        VDATE2 = VDATE2 + 100000000*NCEN
      ENDIF
      IF (VDATE.LT.VDATE1) GO TO 1
      IF (VDATE.GT.VDATE2) GO TO 5
      IF (JSTA.GT.0) GO TO 2
      PRINT 101, IAPUNP
  101 FORMAT ('  *** NO ADP DATA AVAILABLE ON', I5, 3I3,
     &        ', WILL TRY NEXT DATE ***')
      GO TO 1
    2 CONTINUE
      KMAP = -FLAG
      IF(DIAG) PRINT 102, IAPUNP, IAPPCK, KMAP, JSTA
  102 FORMAT (T15, 'THE VERIFICATION DATE IS', I5, 3I3,
     &        '  (PACKED DATE = ', Z10, ')', 5X, 'MAP =', I3, 5X,
     &        'NSTA =', I4, //)
      DO 3 I=1,JSTA
      READ (IUADP,ERR=4,END=5) IDSTN(I), ALON(I), ALAT(I), STI(I),
     &                          STJ(I), NRPT(I), DISTNN(I),
     &                        ((ZTRUV(I,J,K), K=1,4), J=1,5)
    3 CONTINUE
      RETURN

C     QUIT AFTER 10 READ ERRORS WITH 105 STATEMENT --
C     USED FOR TEST 4/24/98 BUT MAY KEEP.
      
    4 CONTINUE
      IF(DIAG) PRINT 1004, FLAG, JSTA
 1004 FORMAT (' FLAG, JSTA =', 2I8)
      PRINT 104,  IUADP, IAPUNP
  104 FORMAT ('  *** READ ERROR ON UNIT', I3, ' WHILE PROCESSING',
     &        ' DATA FOR ', I5, 3I3, ', WILL TRY NEXT DATE ***')
      JERR = JERR + 1
      IF (JERR.LT.10)    GO TO 1
    5 CONTINUE
      IF(DIAG) PRINT 105,  IAPUNP
  105 FORMAT ('  ::: DATE EXCEEDS LAST VERIFICATION DATE SELECTED',
     &        ' OR END OF ADP FILE REACHED AFTER ', I5, 3I3,
     &        ' ... ALL DONE :::')
      IER = 1
      RETURN
      END
